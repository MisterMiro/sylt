
let names _ = do
	let value = "whatever"
	
	let name = value
	let Name = value
	let NAME = value
	let _name = value
	let _name_ = value
	let name123 = value
	let name_123 = value
	
	## names that start with keywords
    let nullary = value
	let trueToLife = value
	let falsetto = value
	let letter = value
	let funambulism = value
	let iffy = value
    let thense = value
	let elsewhere = value
    let whileOnAWheel = value
	let andante = value
	let oracle = value
    let issues = value
	let nothingness = value
	let usingasong = value
    let returned = value
	let doable = value
	let enderman = value
    let modular = value
end

let strings _ = do
	let empty = ""
	let alpha = "abcDEFGHijklMNOPqrstUVWxyz"
	
	## all escape sequences...
	let hex = "\x41"
	let backslash = "\\"
	let quote = "\""
	let tab = "\t"
	let newline = "\n"
	let carriageReturn = "\r"
	let nullEscape = "\0"
	
	## ...translate properly
	ensure(hex is "A")
	ensure(backslash is "\x5c")
	ensure(quote is "\x22")
	ensure(tab is "\x09")
	ensure(newline is "\x0a")
	ensure(carriageReturn is "\x0d")
	ensure(nullEscape is "\x00")
end

let numbers _ = do
	let integer = 123456789
	let fractional = 3.141592
end

let precedence _ = do
	## multiplication > addition
	ensure(5 * 2 + 8 is 18)
	ensure(5 / 2 + 8 is 10.5)
	ensure(5 % 2 + 8 is 9)
	
	ensure(10 * 2 - 8 is 12)
	ensure(10 / 2 - 8 is -3)
	ensure(10 % 2 - 8 is -8)
	
	## addition > comparison
	ensure(1 < 1 + 1)
	ensure(1 <= 1 + 1)
	ensure(2 > 1 - 1)
	ensure(2 >= 1 - 1)
	
	## comparison > equality
	ensure(true is 1 < 2)
	ensure(true is 1 <= 2)
	ensure(true not 1 > 2)
	ensure(true not 1 >= 2)
end

let addition _ = do
	let x = 8
	let y = 12
	
	## correct result
	let sum = 20
	ensure(x + y is sum)
	
	## commutative
	ensure(y + x is sum)
	ensure(x + y is y + x)
end

let subtraction _ = do
	let x = 20
	let y = 12
	
	## correct result
	let difference = 8
	ensure(x - y is difference)
	
	## not commutative
	ensure(y - x not difference)
	ensure(x - y not y - x)
end

let multiplication _ = do
	let x = 5
	let y = 16
	
	## correct result
	let product = 80
	ensure(x * y is product)
	
	## commutative
	ensure(y * x is product)
	ensure(x * y is y * x)
end

let division _ = do
	let x = 33
	let y = 3
	
	## correct result
	let quotient = 11
	ensure(x / y is quotient)
	
	## not commutative
	ensure(y / x not quotient)
	ensure(x / y not y / x)
end

let remainder _ = do
	let x = 17
	let y = 12
	
	## correct result
	let remainder = 5
	ensure(x % y is remainder)
	
	## not commutative
	ensure(y % x not remainder)
	ensure(x % y not y % x)
	
	## remainder is always positive
	ensure(7 % 3 is 1)
	ensure(7 % -3 is 1)
	ensure(-7 % 3 is 2)
	ensure(-7 % -3 is 2)
end

let compoundAssignment _ = do
    let x = 1

    x += 2
    ensure(x is 3)

    x -= 1
    ensure(x is 2)

    x *= 8
    ensure(x is 16)

    x /= 2
    ensure(x is 8)

    x %= 7
    ensure(x is 1)
end

let unaryMinus _ = do
	let x = 25
	ensure(-x is 0 - x)
	
	## double negative
	ensure(--x is x)
	
	## quadruple negative
	ensure(----x is x)
end

let arithmetic _ = do
	let a = 1
	let b = a + 2
	ensure(a + b is a - (-b))
end

let comparison _ = do
	let hi = 100
	let lo = -100
	
	ensure(lo < hi)
	ensure(lo <= hi)
	
	ensure(hi > lo)
	ensure(hi >= lo)
	
	let same = 70
	ensure(same <= same)
	ensure(same >= same)
end

let equality _ = do
	## contains a value of each type
	let values = [
		null        ## Null
		true        ## Bool
		235	        ## Number
		[123]	  	## List
        {"a" = "b"} ## Dict
		"'_'"	  	## String
		fun x -> x	## Func
	]
	
	List.iter(fun skipIndex value -> do
		## equal to itself
		ensure(value is value)
		
		## not equal to a value of any other
		## type
		List.iter(fun index other ->
            if index not skipIndex then ensure(value not other)
            values)
	end values)
	
	## perform more type-specific tests
	ensure(true not false)
	
	ensure([] is [])
	ensure([[]] is [[]])
	ensure([0] not [1])
	
	ensure("" is "")
	ensure("ABC" not "abc")
	
	ensure(Math.pow not Math.sqrt)
end

let boolNot _ = do
	ensure(true is not false)
	ensure(false is not true)
    ensure(not true is false)
    ensure(not false is true)
	ensure(not not true is true)
end

let boolAnd _ = do
	ensure((false and false) is false)
	ensure((false and true) is false)
	ensure((true and false) is false)
	ensure((true and true) is true)
	
	## short-circuit evaluation
	let wouldCrash = fun -> ensure(false)
	false and wouldCrash()
end

let boolOr _ = do
	ensure((false or false) is false)
	ensure((false or true) is true)
	ensure((true or false) is true)
	ensure((true or true) is true)
	
	## short-circuit evaluation
	let wouldCrash = fun -> ensure(false)
	true or wouldCrash()
end

let concat _ = do
    ## combining strings with other strings, or values of other types
    ensure("str" + "str" is "strstr")
    ensure(3 + "str" is "3str")
    ensure("str" + 3 is "str3")

    ## adding elements to the start or end of a list
    ensure([1 2] + 3 is [1 2 3])
    ensure(1 + [2 3] is [1 2 3])

    ## removing elements from the start or end of a list
    ensure([1 2 3] - 1 is [1 2])
    ensure(1 - [1 2 3] is [2 3])

    ## adding a list to another
    ensure([1 2 3] + [4 5 6] is [1 2 3 4 5 6])
    ensure([1 2 3] + [[4 5 6]] is [1 2 3 [4 5 6]])
end

let variables _ = do
	## declaring
	let x = 70
	
	## accessing
	ensure(x is 70)
	
	## shadowing
	let x = x - 2
	ensure(x is 68)
	
	## assigning local
	x <- 14
	ensure(x is 14)
	
	## assigning open upvalue
	let y = 888
	let func = fun -> do
		y <- 333
	end
	func()
	ensure(y is 333)
	
	## assigning closed upvalue
	let out = fun -> do
		let z = 4
		let in = fun ->
			z <- 900
		in
	end
	let z = out()()
	ensure(z is 900)
	
	## chaining assignments
	let a = null
	let b = null
	let c = null
	a <- b <- c <- 32
	ensure(a is 32 and b is 32 and c is 32)
end

let blocks _ = do
	## empty block returns null
	let result = do end
	ensure(result is null)
	
	## last expression is returned...
	let result = do 123 end
	ensure(result is 123)
	
	## ...variables do not break anything...
	let result = do
		let v1 = 13
		let v2 = 17
		123
	end
	ensure(result is 123)
	
	## ...and neither do captured values
	let c = 35
	let result = do
		let v = 9
		let sum = c + v
		123
	end
	ensure(result is 123)
	
	## assigning shadowed variable
	let x = true
	do
		let x = true
		x <- false
		ensure(x is false)
	end
	ensure(x is true)
end

let lists _ = do
	## literal
	let list = [1 2 3]

	## length
	ensure(#[] is 0)
	ensure(#[1] is 1)
	ensure(#[1 2] is 2)
	ensure(#list is 3)

	## concatenating
	let x = []
	x += 3
	ensure(x is [3] and #x is 1)
	x += 7
	ensure(x is [3 7] and #x is 2)
	
	## shrinking
	x -= 1
	ensure(x is [3] and #x is 1)
	x -= 1
	ensure(x is [] and #x is 0)
end

let dicts _ = do
	let dict = {"a" = 1 "b" = 2 "c" = 3}
	
	## count operator
	ensure(#dict is 3)

	## dot access
	ensure(Dict.get("a" dict) is dict.a)
end

let functions _ = do
	## empty body returns null
	let empty = fun -> do end
	ensure(empty() is null)
	
	## argument passing
	let add a b = a + b
	ensure(add(3 4) is 7)
	
	## arguments + local variables
	let add a b = do
		let stackMate = 18
		a + b
	end
	ensure(add(3 4) is 7)
	
	## accessing variable outside of
	## a functions stack window
	let outside = "outside"
	let f = fun -> outside
	ensure(f() is outside)
	
	## moving surrounding context to
	## the heap if necessary
	let f = fun -> do
		let keep = "jewel"
		fun -> keep
	end
	ensure(f()() is "jewel")
	
	## anonymous functions using 'fun'
	let anon = fun r s t -> r * s * t
	ensure(anon(2 4 8) is 64)
	
	## recursion
	let clear list = if list not [] then do
		list -= 1
		clear(list)
	end
		
	let nums = [1 2 3]
	clear(nums)
	ensure(nums is [])
end

let ifElse _ = do
	## implicit nil
	ensure((if false then do end) is null)
	
	## if branch
	let result = if true then "A" else "B"
	ensure(result is "A")
	
	## else branch
	let result = if false then "A" else "B"
	ensure(result is "B")
	
	## else if / chaining
	let i = 10
	let result = if i < 0 then "A"
		else if i > 0 then "B"
		else "C"
	ensure(result is "B")
end

let whileLoop _ = do
	let list = []
	let n = 1
	let result = while n <= 3 do
		list += n
		n += 1
	end
	ensure(result is null)
	ensure(list is [1 2 3])
end

let preludeLib _ = do
	## toString
	ensure(toString(null) is "null")
	ensure(toString(true) is "true" and toString(false) is "false")
	ensure(toString(707) is "707")
	ensure(toString(["bananas" 18 true]) is "[\"bananas\" 18 true]")
    ##ensure(toString({"a" = "b"}) is "{\"a\" = \"b\"}")
	ensure(toString(printLn) is "printLn")
	
	## toNum
	ensure(toNum(null) is 0)
	ensure(toNum(true) is 1)
	ensure(toNum(false) is 0)
	ensure(toNum(112) is 112)
    ensure(toNum([1 2 4]) is 0)
    ensure(toNum({"a" = "b"}) is 0)
	ensure(toNum("1337.25") is 1337.25)
	ensure(toNum("") is 0)
	ensure(toNum("error") is 0)
	ensure(toNum(fun _ -> do end) is 0)
	
	## typeOf
	ensure(typeOf(null) is "Null")
	ensure(typeOf(true) is "Bool")
	ensure(typeOf(1) is "Number")
	ensure(typeOf([]) is "List")
    ensure(typeOf({}) is "Dict")
	ensure(typeOf("") is "String")
	ensure(typeOf(fun -> do end) is "Function")

    ## for
    let nums = []
	for(fun i -> nums += i 3 6)
	ensure(nums is [3 4 5])

	## rep
	let nums = []
	rep(fun i -> nums += i 3)
	ensure(nums is [0 1 2])

    ## bitShiftLeft
    ensure(bitShiftLeft(32 1) is 64)

    ## bitShiftRight
    ensure(bitShiftRight(32 1) is 16)

    ## bitAnd
    ensure(bitAnd(4 32) is 0)

    ## bitOr
    ensure(bitOr(16 32) is 48)

    ## bitXor
    ensure(bitXor(16 24) is 8)

    ## bitNot
    ensure(bitNot(40) is -41)
end

let fileLib _ = do
	let path = "sylt_test_file.txt"
	let str = "Hey\n"
	
	## open + write + close
	let handle = File.open(path File.Mode.Write)
	File.write(str handle)
	File.close(handle)
	
	## size
	let handle = File.open(path File.Mode.Read)
	let size = File.size(handle)
	ensure(size is #str)
	File.close(handle)
	
	## read
	let handle = File.open(path File.Mode.Read)
	let contents = File.read(handle)
	ensure(contents is str)
	File.close(handle)
	
	## del
	File.del(path)
end

let listLib _ = do
	## get + set
	let ls = [1 2 3]
	ensure(List.get(0 ls) is 1)
	ensure(List.get(1 ls) is 2)
	ensure(List.get(2 ls) is 3)

	List.set(0 4 ls)
	List.set(1 5 ls)
	List.set(2 6 ls)
	ensure(ls is [4 5 6])

	## swap
	let ls = [55 33]
	List.swap(0 1 ls)
	ensure(ls is [33 55])

    ## init
    let list = List.init(false 3)
    ensure(list is [false false false])

    ## range
    let list = List.range(1 3)
    ensure(list is [1 2 3])

	## add
	let x = [1 3 4]
	List.add(1 2 x)
	ensure(x is [1 2 3 4])
	
	List.add(-1 5 x)
	ensure(x is [1 2 3 4 5])
	
	let x = [1 2 3]
	List.add(0 1 x)
	ensure(x is [1 1 2 3])
	
	## del
	let x = [1 2 3]
	List.del(1 x)
	ensure(x is [1 3])
	
	List.del(0 x)
	ensure(x is [3])
	
	List.del(0 x)
	ensure(x is [])
	
	## first + last
	let list = ["first" "middle" "last"]
	ensure(List.first(list) is "first")
	ensure(List.last(list) is "last")
	
	## count
	let bits = [1 0 0 1 0 1 1 1]
	ensure(List.count(1 bits) is 5)
	
	## contains
	let list = ["a" "B" "c"]
	ensure(List.contains("B" list))
	ensure(not List.contains("b" list))

    ## find
    let list = [1 2 3]
    ensure(List.find(2 list) is 1)

    ## sort
    let list = [2 3 1]
    ensure(List.sort(list) is [1 2 3])
    ensure(list is [2 3 1])
	
	## rev
	let normal = [1 2 3]
	ensure(List.rev(normal) is [3 2 1])
	ensure(List.rev([]) is [])
	ensure(List.rev([1]) is [1])
	
	## iter
	let indices = [0 1 2]
	let copy = []

	List.iter(fun index value -> do
		ensure(index is List.get(index indices))
		copy += value
	end
	indices)

	ensure(indices is copy)
	
	## forEach
	let items = [1 2 3]
	let copy = []
	List.forEach(fun index -> copy += index items)
	ensure(items is copy)
	
	## map
	##let list = List.map(fun n -> n * 2 [4 8 16])
	##ensure(list is [8 16 32])
	
	## filter
	let list = [1 0 0 1 1 0]
	list <- List.filter(Num.isOne list)
	ensure(list is [1 1 1])
	
	## fold
	let list = [2 4 6]
	ensure(List.fold(Math.mul 1 list) is 48)
	
	## sum
	let list = [1 2 3]
	ensure(List.sum(list) is 6)
	
	## average
	let list = [1 2 3]
	ensure(List.average(list) is 2)
	ensure(List.average([]) is 0)
	
	## mean
	let vals = [0 5 10 0]
	ensure(List.mean(vals) is 7.5)
	
	## flatten
	let mess = [1 [2 [3 4]] 5]
	let clean = List.flatten(mess)
	ensure(clean is [1 2 3 4 5])
	
	## dedup
	let list = [7 7 8 9 9]
	ensure(List.dedup(list) is [7 8 9])
	
	## any
	let set = [true true false]
	ensure(List.any(fun v -> v set))
	ensure(List.any(fun v -> not v set))
	set -= 1
	ensure(List.any(fun v -> v set))
	ensure(not List.any(fun v -> not v set))
	
	## all
	let set = [5 7]
	ensure(List.all(fun n -> n >= 5 set))
	ensure(List.all(fun n -> not (n < 5) set))
	set <- set + 3
	ensure(not List.all(fun n -> n >= 5 set))
	ensure(not List.all(fun n -> not (n < 5) set))
end

let dictLib _ = do
	let dict = {
		"door" = "window"
		"car" = "helicopter"
		"box" = "pyramid"
	}
		
	## keys
	ensure(Dict.keys(dict) is ["door" "car" "box"])
		
	## values
	ensure(Dict.values(dict) is ["window" "helicopter" "pyramid"])
end

let stringLib _ = do
	## alpha
	let str = "abcdefghijklmnopqrstuvwxyz"
	ensure(String.letters is str)
	
	## digits
	let str = "0123456789"
	ensure(String.digits is str)

	## punctuation
	let str = "!\"#$%&'()*+,-./:;<=>?@[\\]^_`{|}~"
	ensure(String.punctuation is str)
	
	## length
	ensure(#String.digits is 10)

	## chars + join
	ensure(String.chars("Abc") is ["A" "b" "c"])
	ensure(String.join(["A" "b" "c"]) is "Abc")
	ensure(String.join([1 "2" 34 "5"]) is "12345")
		
	## split
	ensure(String.split([""] "lol") is ["lol"])
	ensure(String.split([" "] "lol") is ["lol"])
	ensure(String.split([" "] "abc rst xyz") is ["abc" "rst" "xyz"])
		
	## lowercase + uppercase + swapCase
	let str = "MiXeD!"
	ensure(String.lowercase(str) is "mixed!")
	ensure(String.uppercase(str) is "MIXED!")
	ensure(String.swapcase(str) is "mIxEd!")

	## isLower + isUpper + isWhitespace
	ensure(String.isLower("x") and not String.isLower("X"))
	ensure(String.isUpper("X") and not String.isUpper("x"))
	ensure(String.isWhitespace(" \t\n\r"))
	
	## startsWith
	ensure(String.startsWith("abc" String.letters))
	ensure(not String.startsWith("xyz" String.letters))
		
	## endsWith
	ensure(String.endsWith("xyz" String.letters))
	ensure(not String.endsWith("abc" String.letters))
		
	## trimStart
	ensure(String.trimStart("\t\n hi") is "hi")
	ensure(String.trimStart("hi") is "hi")
	
	## trimEnd
	ensure(String.trimEnd("hi \n\t") is "hi")
	ensure(String.trimEnd("hi") is "hi")
	
	## trim
	ensure(String.trim("\n\t hi \n\t") is "hi")
	ensure(String.trim("hi") is "hi")
	
	## replace
	let str = "Country: SE"
	str <- String.replace("SE" "Sweden" str)
	ensure(str is "Country: Sweden")
	
	str <- String.replace("Sweden" "SE" str)
	ensure(str is "Country: SE")
	
	str <- "leet"
	str <- String.replace("l" "1" str)
	str <- String.replace("e" "3" str)
	str <- String.replace("t" "7" str)
	ensure(str is "1337")
	
	## leftPad
	ensure(String.leftPad(10 "-=" "test") is "-=-=-=test")

	## rightPad
	ensure(String.rightPad(10 "*" "test") is "test******")
	
	## centerPad
	ensure(String.centerPad(10 "|" "test") is "|||test|||")
end

let mathLib _ = do
	## pi
	ensure(Num.eq(Math.pi 3.141592 0.00001))
	
	## euler's number
	ensure(Num.eq(Math.e 2.718281 0.00001))
	
	## signum
	ensure(Math.signum(-80) is Math.Sign.Negative)
	ensure(Math.signum(0) is Math.Sign.Zero)
	ensure(Math.signum(80) is Math.Sign.Positive)
	
	## abs
	ensure(Math.abs(-8) is 8 and Math.abs(8) is 8)
	
	## log + pow
	let target = 8
	let bases = [
		## common
		2 Math.e 10
		## uncommon
		3 14 2000 18000 2000000
	]
	
	List.forEach(fun base -> do
		let power = Math.log(target base)
		let result = Math.pow(base power)
		ensure(Num.eq(result target 0.0000001))
	end
    bases)
	
	## sqrt
	ensure(Math.sqrt(256) is 16)
	ensure(Math.pow(32 0.5) is Math.sqrt(32))
	
	## min + max
	ensure(Math.min(-1 1) is -1)
	ensure(Math.max(-1 1) is 1)
	
	## clamp
	let x = 2
	ensure(Math.clamp(x 0 - 5 5) is x)
	
	let x = 20
	ensure(Math.clamp(x 1 4) is 4)
	
	let x = -12
	ensure(Math.clamp(x 0 - 8 3) is -8)
	
	## lerp
	ensure(Math.lerp(-10 10 0) is -10)
	ensure(Math.lerp(-10 10 0.25) is -5)
	ensure(Math.lerp(-10 10 0.5) is 0)
	ensure(Math.lerp(-10 10 0.75) is 5)
	ensure(Math.lerp(-10 10 1) is 10)
	
	## floor + ceil + round
	let checkRounding n = do
		let low = n + 0.2
		let mid = n + 0.5
		let high = n + 0.8
	
		ensure(Math.floor(low) is n)
		ensure(Math.floor(high) is n)
		ensure(Math.ceil(low) is n + 1)
		ensure(Math.ceil(high) is n + 1)
		ensure(Math.round(low) is n)
		ensure(Math.round(high) is n + 1)
	
		ensure(Math.round(mid) is if n < 0 then n else n + 1)
	end
	
	List.forEach(checkRounding [-1 0 1])
	
	## rad + deg
	ensure(Num.eq(Math.rad(1) 0.0175 0.001))
	ensure(Num.eq(Math.deg(1) 57.296 0.001))
	
	## sin + cos + tan
	ensure(Num.eq(Math.sin(90) 0.8939 0.001))
	ensure(Num.eq(Math.cos(90) 0 - 0.4480 0.001))
	ensure(Num.eq(Math.tan(90) 0 - 1.9952 0.001))
			
	## asin + acos + atan
	ensure(Num.eq(Math.asin(1) 1.5708 0.001))
	ensure(Num.eq(Math.acos(1) 0 0.001))
	ensure(Num.eq(Math.atan(1) 0.785398 0.001))
		
	## sinh + cosh + tanh
	ensure(Num.eq(Math.sinh(1) 1.1752 0.001))
	ensure(Num.eq(Math.cosh(1) 1.5430 0.001))
	ensure(Num.eq(Math.tanh(1) 0.7615 0.001))
	
	## asinh + acosh + atanh
	ensure(Num.eq(Math.asinh(0.8) 0.7326 0.001))
	ensure(Num.eq(Math.acosh(1.2) 0.6223 0.001))
	ensure(Num.eq(Math.atanh(0.8) 1.0986 0.001))
end

let numLib _ = do
    ## eq
	let val = 0.005
	let epsilon = 0.01
	
	ensure(Num.eq(0 val epsilon))
	ensure(Num.eq(0 epsilon - 0.0000001 epsilon))
	ensure(not Num.eq(0 epsilon epsilon))

    ## half
    ensure(Num.half(2) is 1)

    ## twice
    ensure(Num.twice(1) is 2)

    ## squared
    ensure(Num.squared(4) is 16)

    ## isZero + isOne
    ensure(Num.isZero(0))
    ensure(not Num.isZero(1))
    ensure(Num.isOne(1))
    ensure(not Num.isOne(0))

    ## isOdd + isEven
    ensure(Num.isOdd(3))
    ensure(not Num.isOdd(4))
    ensure(Num.isEven(4))
    ensure(not Num.isEven(3))

    ## isPositive + isNegative
    ensure(Num.isPositive(10))
    ensure(not Num.isPositive(-10))
    ensure(Num.isNegative(-10))
    ensure(not Num.isNegative(10))
end

let vec3Lib _ = do
    ## zero + one
    ensure(Vec3.zero() is Vec3.scalar(0))
    ensure(Vec3.one() is Vec3.scalar(1))

    ## dot + cross
    let a = Vec3.new(8 3 7)
    let b = Vec3.new(5 4 10)
    ensure(Vec3.dot(a b) is 122)
    ensure(Vec3.cross(a b) is Vec3.new(2 0 - 45 17))

    ## sizeSquared + size
    ensure(Vec3.sizeSquared(Vec3.new(10 0 0)) is 100)
    ensure(Vec3.size(Vec3.new(10 0 0)) is 10)

    ## norm
    let norm = Vec3.norm(Vec3.new(10 5 2.5))
    ensure(Num.eq(norm.x 0.8728 0.0001))
    ensure(Num.eq(norm.y 0.4364 0.0001))
    ensure(Num.eq(norm.z 0.2182 0.0001))

    ## dir
    let a = Vec3.new(5 0 0)
    let b = Vec3.new(0 0 0)
    ensure(Vec3.dir(a b) is Vec3.new(-1 0 0))

    ## add + sub + mul + div
    let vec = Vec3.add(Vec3.new(-2 8 5) Vec3.new(4 2 5))
    ensure(vec is Vec3.new(2 10 10))

    vec <- Vec3.sub(vec Vec3.new(1 4 8))
    ensure(vec is Vec3.new(1 6 2))

    vec <- Vec3.mul(vec Vec3.new(2 3 4))
    ensure(vec is Vec3.new(2 18 8))

    vec <- Vec3.div(vec Vec3.new(2 2 2))
    ensure(vec is Vec3.new(1 9 4))
end

## tests to run

let tests = List.flatten([
	"Parsing"	## category name
	[			## functions to test
		names
		strings
		numbers
		precedence
	]
	"Operators"
	[
		addition
		subtraction
		multiplication
		division
		remainder
        compoundAssignment
		unaryMinus
		arithmetic
		comparison
		equality
		boolNot
		##boolAnd
		boolOr
		concat
	]
	"General"
	[
		variables
		blocks
		lists
		dicts
		functions
		ifElse
		whileLoop
	]
	"Standard library"
	[
		preludeLib
		listLib
		dictLib
		stringLib
        fileLib
		mathLib
        numLib
        vec3Lib
	]
])

## let's go!
List.forEach(fun test ->
	if typeOf(test) is "String" then
		printLn(test + ":")
	else do
		let name = String.rightPad(20 "." toString(test))
		print("  " + name)
		
		test(null)
		printLn("OK")
	end
	tests)

printLn("All tests passed!")
