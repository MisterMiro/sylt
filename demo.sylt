## here's a comment

## declare a variable using 'let'
let x = 5
	
## modify its value using '<-'
x <- 10

## use it in an expression
x + 5 / 2 

## arithmetic operators:
## +     addition
## -     subtraction
## *     multiplication
## /     division
## %     remainder
	
## declare a function by adding a set of
## parentheses, optionally containing a comma-
## separated parameter list, after the name.
## the expression after the equal sign
## is the function body, it will not be
## executed until the function is called
	
let addNumbers(a, b) = a + b

## call a function
	
let result = addNumbers(5, 3)
ensure(result = 8) ## errors if not true

## use a block expression for multiple lines
## of code. the last expression in a block
## is returned implicitly

let multiplyBySomething(x) = {
	let factor = 8
	x * 8
}

## an empty block returns 'nil'

let empty = {}

## nil is the value that represents nothing
## it is mainly used when there is no
## meaningful value for an expression to
## return

let nilVal = nil
	
## anonymous functions (lambdas) can be
## created using the 'fun' keyword.
## functionally they're identical to regular
## functions
	
rep(3, fun(n) -> putLn((n + 1) * 2.5))
	
## prints:
## 2.5
## 5
## 7.5

## booleans

## a boolean is either true or false.
## the comparison and equality operators
## return booleans

## comparison operators:
## <     less than
## <=    less than or equal to
## >     greater
## >=    greater than or equal to

## equality operators:
## =     is equal to
## !=    is not equal to
	
## branch using 'if/else'

let greaterThanHalf(n) =
	if (n > 0.5) "hi"
	else "lo"
	
## an if without a matching else will return
## nil if the condition evaluates to false

let result = if (1 > 2) "hi"

## is the same as:

let result = if (1 > 2) "hi" else nil
	
## example of a loop using recursion
	
let countdown(i) =
	if (i > 0) {
		putLn(i)
		countdown(i - 1)
	}
	
countdown(3)
			
## prints:
## 3
## 2
## 1
## 0

## some example functions:

let addOne(n) = n + 1
let timesTwo(n) = n * 2

## geometry
	
let rectIsSquare(w, h) = (w = h)	
let rectArea(w, h) = w * h
let circleIsRound() = true	
let circleArea(radius) = PI * raise(radius, 2)
	
## Ohm's law

let calcVoltage(current, resistance) =
	current * resistance
		
let calcCurrent(voltage, resistance) =
	voltage / resistance
		
let calcResistance(voltage, current) =
	voltage / current

## strings

## a string literal consists of any series of
## characters inside two doublequotes

let str = "Some text here"

## strings may contain escape sequences,
## which are special combinations of
## characters that translate to a byte

## all escape sequences begin with a
## backslash

## they are:
## \xff   hexadecimal byte
## \\     backslash
## \"     quote
## \t     tab
## \n     newline
## \r     carriage return
## \0     null terminator

## example:
let newline = "two\nlines"

## to create a list:
let list = [1, 2, 3]

## getting value at index
list[0]

## storing value at index
list[0] <- 10

## negative index counts from the end
ensure(list[-1] = list[List.length(list) - 1])
