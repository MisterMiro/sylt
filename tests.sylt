
let names() = {
	let value = "whatever"
	
	let name = value
	let Name = value
	let NAME = value
	let _name = value
	let _name_ = value
	let name123 = value
	let name_123 = value
	
	## names that start with keywords
	let nilpotent = value
	let true_to_life = value
	let falsetto = value
	let letter = value
	let funambulism = value
	let iffy = value
	let elsewhere = value
	let andante = value
	let oracle = value
	let usingasong = value
}

let strings() = {
	let empty = ""
	let alpha = "abcDEFGHijklMNOPqrstUVWxyz"
	
	## all escape sequences...
	let hex = "\x41"
	let backslash = "\\"
	let quote = "\""
	let tab = "\t"
	let newline = "\n"
	let carriage_return = "\r"
	let null = "\0"
	
	## ...translate properly
	ensure(hex = "A")
	ensure(backslash = "\x5c")
	ensure(quote = "\x22")
	ensure(tab = "\x09")
	ensure(newline = "\x0a")
	ensure(carriage_return = "\x0d")
	ensure(null = "\x00")
}

let numbers() = {
	let integer = 123456789
	let fractional = 3.141592
}

let precedence() = {
	## multiplication > addition
	ensure(5 * 2 + 8 = 18)
	ensure(5 / 2 + 8 = 10.5)
	ensure(5 % 2 + 8 = 9)
	
	ensure(10 * 2 - 8 = 12)
	ensure(10 / 2 - 8 = -3)
	ensure(10 % 2 - 8 = -8)
	
	## addition > comparison
	ensure(1 < 1 + 1)
	ensure(1 <= 1 + 1)
	ensure(2 > 1 - 1)
	ensure(2 >= 1 - 1)
	
	## comparison > equality
	ensure(true = 1 < 2)
	ensure(true = 1 <= 2)
	ensure(true != 1 > 2)
	ensure(true != 1 >= 2)
}

let addition() = {
	let x = 8
	let y = 12
	
	## correct result
	let sum = 20
	ensure(x + y = sum)
	
	## commutative
	ensure(y + x = sum)
	ensure(x + y = y + x)
}

let subtraction() = {
	let x = 20
	let y = 12
	
	## correct result
	let difference = 8
	ensure(x - y = difference)
	
	## not commutative
	ensure(y - x != difference)
	ensure(x - y != y - x)
}

let multiplication() = {
	let x = 5
	let y = 16
	
	## correct result
	let product = 80
	ensure(x * y = product)
	
	## commutative
	ensure(y * x = product)
	ensure(x * y = y * x)
}

let division() = {
	let x = 33
	let y = 3
	
	## correct result
	let quotient = 11
	ensure(x / y = quotient)
	
	## not commutative
	ensure(y / x != quotient)
	ensure(x / y != y / x)
}

let remainder() = {
	let x = 17
	let y = 12
	
	## correct result
	let remainder = 5
	ensure(x % y = remainder)
	
	## not commutative
	ensure(y % x != remainder)
	ensure(x % y != y % x)
	
	## remainder is always positive
	ensure(7 % 3 = 1)
	ensure(7 % -3 = 1)
	ensure(-7 % 3 = 2)
	ensure(-7 % -3 = 2)
}

let unaryMinus() = {
	let x = 25
	ensure(-x = 0 - x)
	
	## double negative
	ensure(--x = x)
	
	## quadruple negative
	ensure(----x = x)
}

let arithmetic() = {
	let a = 1
	let b = a + 2
	ensure(a + b = a - (-b))
}

let comparison() = {
	let hi = 100
	let lo = -100
	
	ensure(lo < hi)
	ensure(lo <= hi)
	
	ensure(hi > lo)
	ensure(hi >= lo)
	
	let same = 70
	ensure(same <= same)
	ensure(same >= same)
}

let equality() = {
	## contains a value of each type
	let values = [
		nil,        	## Nil
		true,       	## Bool
		235,	    	## Num
		[123],	  	## List
		"'_'",	  	## String
		fun(x) -> x,	## Func
	]
	
	List.iter(fun(skip, value) -> {
		## equal to itself
		ensure(value = value)
		
		## not equal to a value of any other
		## type
		List.iter(fun(index, other) ->
			if (index != skip)
				ensure(value != other),
		values)
	}, values)
	
	## perform more type-specific tests
	ensure(true != false)
	
	ensure([] = [])
	ensure([[]] = [[]])
	ensure([0] != [1])
	
	ensure("" = "")
	ensure("ABC" != "abc")
	
	ensure(Math.pow != Math.sqrt)
}

let boolNot() = {
	ensure(true = !false)
	ensure(false = !true)
	ensure(!!true = true)
}

let boolAnd() = {
	ensure((false and false) = false)
	ensure((false and true) = false)
	ensure((true and false) = false)
	ensure((true and true) = true)
	
	## short-circuit evaluation
	let would_crash() = ensure(false)
	false and would_crash()
}

let boolOr() = {
	ensure((false or false) = false)
	ensure((false or true) = true)
	ensure((true or false) = true)
	ensure((true or true) = true)
	
	## short-circuit evaluation
	let would_crash() = ensure(false)
	true or would_crash()
}

let concat() = {
	## String
	ensure("Hello " ++ "Word" = "Hello Word")
	
	## List
	ensure([1, 2] ++ [3, 4] = [1, 2, 3, 4])
}

let variables() = {
	## declaring
	let x = 70
	
	## accessing
	ensure(x = 70)
	
	## shadowing
	let x = x - 2
	ensure(x = 68)
	
	## assigning local
	x <- 14
	ensure(x = 14)
	
	## assigning open upvalue
	let y = 888
	let func() = {
		y <- 333
	}
	func()
	ensure(y = 333)
	
	## assigning closed upvalue
	let out() = {
		let z = 4
		let in() =
			z <- 900
		in
	}
	let z = out()()
	ensure(z = 900)
	
	## chaining assignments
	let a = nil
	let b = nil
	let c = nil
	a <- b <- c <- 32
	ensure(a = 32 and b = 32 and c = 32)
}

let blocks() = {
	## empty block returns nil
	let result = {}
	ensure(result = nil)
	
	## last expression is returned...
	let result = {123}
	ensure(result = 123)
	
	## ...variables do not break anything...
	let result = {
		let v1 = 13
		let v2 = 17
		123
	}
	ensure(result = 123)
	
	## ...and neither do captured values
	let c = 35
	let result = {
		let v = 9
		let sum = c + v
		123
	}
	ensure(result = 123)
	
	## assigning shadowed variable
	let x = true
	{
		let x = nil
		x <- false
		ensure(x = false)
	}
	ensure(x = true)
}

let lists() = {
	let list = [1, 2, 3]
	
	## indexing
	ensure(list[0] = 1
		and list[1] = 2
		and list[2] = 3)
		
	## negative indices count from the end
	## i.e. index -1 is equivalent to len - 1
	ensure(list[1] = list[-2])
	
	## assignment
	list[0] <- list[-1]
	list[1] <- list[-1]
	ensure(list = [3, 3, 3])
}

let dicts() = {
	let dict = [|"a": 1, "b": 2, "c": 3|]
	
	## dot access
	ensure(dict["a"] = dict.a)
}

let functions() = {
	## empty body returns nil
	let empty() = {}
	ensure(empty() = nil)
	
	## argument passing
	let add(a, b) = a + b
	ensure(add(3, 4) = 7)
	
	## arguments + local variables
	let add(a, b) = {
		let stack_mate = 18
		a + b
	}
	ensure(add(3, 4) = 7)
	
	## accessing variable outside of
	## a functions stack window
	let outside = "outside"
	let f() = outside
	ensure(f() = outside)
	
	## moving surrounding context to
	## the heap if necessary
	let f() = {
		let keep = "jewel"
		let f2() = keep
		f2
	}
	ensure(f()() = "jewel")
	
	## anonymous functions using 'fun'
	let anon = fun(r, s, t) -> r * s * t
	ensure(anon(2, 4, 8) = 64)
	
	## recursion
	let clear(ls) =
		if (ls != []) {
			List.pop(ls)
			clear(ls)
		}
		
	let nums = [1, 2, 3]
	clear(nums)
	ensure(nums = [])
}

let ifElse() = {
	## implicit nil
	ensure((if (false) {}) = nil)
	
	## if branch
	let result = if (true) "A" else "B"
	ensure(result = "A")
	
	## else branch
	let result = if (false) "A" else "B"
	ensure(result = "B")
	
	## else if / chaining
	let i = 10
	let result =
		if (i < 0) "A"
		else if (i > 0) "B"
		else "C"
	ensure(result = "B")
}

let whileLoop() = {
	let list = []
	let n = 1
	let res = while (n <= 3) {
		List.push(list, n)
		n <- n + 1
	}
	ensure(res = nil)
	ensure(list = [1, 2, 3])
}

let preludeLib() = {
	## toString
	ensure(toString(nil) = "nil")
	ensure(toString(true) = "true"
		and toString(false) = "false")
	ensure(toString(707) = "707")
	ensure(toString(["bananas", 18, true])
		= "[\"bananas\", 18, true]")
	ensure(toString(printLn) = "printLn")
	
	## toNum
	ensure(toNum(nil) = 0)
	ensure(toNum(true) = 1)
	ensure(toNum(false) = 0)
	ensure(toNum(112) = 112)
	ensure(toNum("1337.25") = 1337.25)
	ensure(toNum("") = 0)
	ensure(toNum("error") = 0)
	ensure(toNum([1, 2, 4]) = 0)
	ensure(toNum(fun(_) -> {}) = 0)
	
	## typeOf
	ensure(typeOf(nil) = "Nil")
	ensure(typeOf(true) = "Bool")
	ensure(typeOf(1) = "Num")
	ensure(typeOf([]) = "List")
	ensure(typeOf("") = "String")
	ensure(typeOf(fun() -> {}) = "Function")
	
	## rep
	let nums = []
	rep(fun(i) ->
		List.push(nums, (i + 1) * 3), 3)
	ensure(nums = [3, 6, 9])
}

let fileLib() = {
	let path = "sylt_test_file.txt"
	let str = "Hey\n"
	
	## open + write + close
	let handle = File.open(path, "w")
	File.write(handle, str)
	File.close(handle)
	
	## size
	let handle = File.open(path, "r")
	let size = File.size(handle)
	ensure(size = String.length(str))
	File.close(handle)
	
	## read
	let handle = File.open(path, "r")
	let contents = File.read(handle, size)
	ensure(contents = str)
	File.close(handle)
	
	## del
	File.del(path)
}

let listLib() = {
	## length
	ensure(List.length([]) = 0)
	ensure(List.length([1]) = 1)
	ensure(List.length([1, 2]) = 2)
	
	## add
	let x = [1, 3, 4]
	List.add(x, 1, 2)
	ensure(x = [1, 2, 3, 4])
	
	List.add(x, -1, 5)
	ensure(x = [1, 2, 3, 4, 5])
	
	let x = [1, 2, 3]
	List.add(x, 0, 1)
	ensure(x = [1, 1, 2, 3])
	
	## del
	let x = [1, 2, 3]
	List.del(x, 1)
	ensure(x = [1, 3])
	
	List.del(x, 0)
	ensure(x = [3])
	
	List.del(x, 0)
	ensure(x = [])
	
	## push + pop
	let x = []
	
	List.push(x, 3) ## = [3]
	ensure(x = [3] and List.length(x) = 1)
	List.push(x, 7) ## = [3, 7]
	ensure(x = [3, 7] and List.length(x) = 2)
	
	List.pop(x) ## = [3]
	ensure(x = [3] and List.length(x) = 1)
	List.pop(x) ## = []
	ensure(x = [] and List.length(x) = 0)
	
	## first + last
	let list = ["first", "middle", "last"]
	ensure(List.first(list) = "first")
	ensure(List.last(list) = "last")
	
	## count
	let bits = [1, 0, 0, 1, 0, 1, 1, 1]
	ensure(List.count(bits, 1) = 5)
	
	## contains
	let list = ["a", "B", "c"]
	ensure(List.contains(list, "B"))
	ensure(!List.contains(list, "b"))
	
	## rev
	let normal = [1, 2, 3]
	ensure(List.rev(normal) = [3, 2, 1])
	ensure(List.rev([]) = [])
	ensure(List.rev([1]) = [1])
	
	## range
	let a = List.range(-3, 3)
	let b = [-3, -2, -1, 0, 1, 2, 3]
	ensure(a = b)
	
	## iter
	let indices = [0, 1, 2]
	let copy = []
	List.iter(fun(index, value) -> {
		ensure(index = indices[index])
		List.push(copy, value)
	}, indices)
	ensure(indices = copy)
	
	## forEach
	let items = [1, 2, 3]
	let copy = []
	List.forEach(fun(value) ->
		List.push(copy, value), items)
	ensure(items = copy)
	
	## map
	let list = List.map(twice, [4, 8, 16])
	ensure(list = [8, 16, 32])
	
	## filter
	let list = [1, 0, 0, 1, 1, 0]
	list <- List.filter(isOne, list)
	ensure(list = [1, 1, 1])
	
	## fold
	let list = [2, 4, 6]
	ensure(List.fold(mul, 1, list) = 48)
	
	## sum
	let list = [1, 2, 3]
	ensure(List.sum(list) = 6)
	
	## average
	let list = [1, 2, 3]
	ensure(List.average(list) = 2)
	ensure(List.average([]) = 0)
	
	## mean
	let vals = [0, 5, 10, 0]
	ensure(List.mean(vals) = 7.5)
	
	## flatten
	let mess = [1, [2, [3, 4]], 5]
	let clean = List.flatten(mess)
	ensure(clean = [1, 2, 3, 4, 5])
	
	## dedup
	let list = [7, 7, 8, 9, 9]
	ensure(List.dedup(list) = [7, 8, 9])
	
	## any
	let set = [true, true, false]
	ensure(List.any(fun(v) -> v, set))
	ensure(List.any(fun(v) -> !v, set))
	List.pop(set) ## = [true, true]
	ensure(List.any(fun(v) -> v, set))
	ensure(!List.any(fun(v) -> !v, set))
	
	## all
	let set = [5, 7]
	ensure(List.all(fun(n) -> n >= 5, set))
	ensure(List.all(fun(n) -> !(n < 5), set))
	List.push(set, 3) ## = [5, 7, 3]
	ensure(!List.all(fun(n) -> n >= 5, set))
	ensure(!List.all(fun(n) -> !(n < 5), set))
}

let dictLib() = {
	let dict = [|
		"door": "window",
		"car": "helicopter",
		"box": "pyramid",
	|]
	
	## length
	ensure(Dict.length(dict) = 3)
	
	## keys
	ensure(Dict.keys(dict) =
		["door", "car", "box"])
		
	## values
	ensure(Dict.values(dict) =
		["window", "helicopter", "pyramid"])
}

let stringLib() = {
	## alpha
	let str = "abcdefghijklmnopqrstuvwxyz"
	ensure(String.alpha = str)
	
	## digits
	let str = "0123456789"
	ensure(String.digits = str)
	
	## length
	ensure(String.length(String.digits) = 10)

	## chars + join
	ensure(String.chars("Abc")
		= ["A", "b", "c"])
	ensure(String.join(["A", "b", "c"])
		= "Abc")
	ensure(String.join([1, "2", 34, "5"])
		= "12345")
		
	## split
	ensure(String.split("lol", "") = ["lol"])
	ensure(String.split("lol", " ") = ["lol"])
	ensure(String.split("abc,rst,xyz", ",")
		= ["abc", "rst", "xyz"])
		
	## lower + upper + swapCase
	let str = "MiXeD!"
	ensure(String.lower(str) = "mixed!")
	ensure(String.upper(str) = "MIXED!")
	ensure(String.swapCase(str) = "mIxEd!")

	## isLower + isUpper + isWhitespace
	ensure(String.isLower("x") and !String.isLower("X"))
	ensure(String.isUpper("X") and !String.isUpper("x"))
	ensure(String.isWhitespace(" \t\n\r"))
	
	## startsWith
	ensure(String.startsWith(
		String.alpha, "abc"))
	ensure(!String.startsWith(
		String.alpha, "xyz"))
		
	## endsWith
	ensure(String.endsWith(
		String.alpha, "xyz"))
	ensure(!String.endsWith(
		String.alpha, "abc"))
		
	## trimStart
	ensure(String.trimStart("\t\n hi") = "hi")
	ensure(String.trimStart("hi") = "hi")
	
	## trimEnd
	ensure(String.trimEnd("hi \n\t") = "hi")
	ensure(String.trimEnd("hi") = "hi")
	
	## trim
	ensure(String.trim("\n\t hi \n\t") = "hi")
	ensure(String.trim("hi") = "hi")
	
	## replace
	let str = "Country: SE"
	str <- String.replace(str,
		"SE", "Sweden")
	ensure(str = "Country: Sweden")
	
	str <- String.replace(str,
		"Sweden", "SE")
	ensure(str = "Country: SE")
	
	str <- "leet"
	str <- String.replace(str, "l", "1")
	str <- String.replace(str, "e", "3")
	str <- String.replace(str, "t", "7")
	ensure(str = "1337")
	
	## leftPad
	ensure(String.leftPad("test", 10, "-=")
		= "-=-=-=test")

	## rightPad
	ensure(String.rightPad("test", 10, "*")
		= "test******")
	
	## centerPad
	ensure(String.centerPad("test", 10, "|")
		= "|||test|||")
		
	## fmt
	ensure(String.fmt("x=%1 + 2%") = "x=3")
}

let mathLib() = {
	## pi
	ensure(floatEq(
		Math.pi, 3.141592, 0.00001))
	
	## euler's number
	ensure(floatEq(
		Math.e, 2.718281, 0.00001))
	
	## floatEq
	let val = 0.005
	let epsilon = 0.01
	
	ensure(floatEq(0, val, epsilon))
	ensure(floatEq(
		0, epsilon - 0.0000001, epsilon))
	ensure(!floatEq(
		0, epsilon, epsilon))
	
	## numSign
	ensure(Math.numSign(-80) = -1)
	ensure(Math.numSign(0) = 0)
	ensure(Math.numSign(80) = 1)
	
	## abs
	ensure(Math.abs(-8) = 8
		and Math.abs(8) = 8)
	
	## log + pow
	let target = 8
	let bases = [
		## common
		2, Math.e, 10,
		## uncommon
		3, 14, 2000, 18000, 2000000,
	]
	
	let testLog(base, target) = {
		let power = Math.log(target, base)
		let result = Math.pow(base, power)
		ensure(floatEq(
			result, target, 0.0000001))
	}
	
	List.forEach(fun(base) ->
		testLog(base, target), bases)
	
	## sqrt
	ensure(Math.sqrt(256) = 16)
	ensure(Math.pow(32, 0.5) = Math.sqrt(32))
	
	## min + max
	ensure(Math.min(-1, 1) = -1)
	ensure(Math.max(-1, 1) = 1)
	
	## clamp
	let x = 2
	ensure(Math.clamp(x, -5, 5) = x)
	
	let x = 20
	ensure(Math.clamp(x, 1, 4) = 4)
	
	let x = -12
	ensure(Math.clamp(x, -8, 3) = -8)
	
	## lerp
	ensure(Math.lerp(-10, 10, 0) = -10)
	ensure(Math.lerp(-10, 10, 0.25) = -5)
	ensure(Math.lerp(-10, 10, 0.5) = 0)
	ensure(Math.lerp(-10, 10, 0.75) = 5)
	ensure(Math.lerp(-10, 10, 1) = 10)
	
	## floor + ceil + round
	let checkRounding(n) = {
		let low = n + 0.2
		let mid = n + 0.5
		let high = n + 0.8
	
		ensure(Math.floor(low) = n)
		ensure(Math.floor(high) = n)
	
		ensure(Math.ceil(low) = n + 1)
		ensure(Math.ceil(high) = n + 1)
	
		ensure(Math.round(low) = n)
		ensure(Math.round(high) = n + 1)
	
		ensure(Math.round(mid) =
			if (n < 0) n
			else n + 1
		)
	}
	
	List.forEach(checkRounding, [-1, 0, 1])
	
	## rad + deg
	ensure(floatEq(
		Math.rad(1), 0.0175, 0.001))
	ensure(floatEq(
		Math.deg(1), 57.296, 0.001))
	
	## sin + cos + tan
	ensure(floatEq(
		Math.sin(90), 0.8939, 0.001))
	ensure(floatEq(
		Math.cos(90), -0.4480, 0.001))
	ensure(floatEq(
		Math.tan(90), -1.9952, 0.001))
			
	## asin + acos + atan
	ensure(floatEq(
		Math.asin(1), 1.5708, 0.001))
	ensure(floatEq(
		Math.acos(1), 0, 0.001))
	ensure(floatEq(
		Math.atan(1), 0.785398, 0.001))
		
	## sinh + cosh + tanh
	ensure(floatEq(
		Math.sinh(1), 1.1752, 0.001))
	ensure(floatEq(
		Math.cosh(1), 1.5430, 0.001))
	ensure(floatEq(
		Math.tanh(1), 0.7615, 0.001))
	
	## asinh + acosh + atanh
	ensure(floatEq(
		Math.asinh(0.8), 0.7326, 0.001))
	ensure(floatEq(
		Math.acosh(1.2), 0.6223, 0.001))
	ensure(floatEq(
		Math.atanh(0.8), 1.0986, 0.001))
}

## tests to run

let tests = List.flatten([
	"Parsing",	## category name
	[			 ## functions to test
		names,
		strings,
		numbers,
		precedence,
	],
	"Operators",
	[
		addition,
		subtraction,
		multiplication,
		division,
		remainder,
		unaryMinus,
		arithmetic,
		comparison,
		equality,
		boolNot,
		boolAnd,
		boolOr,
		concat,
	],
	"General",
	[
		variables,
		blocks,
		lists,
		dicts,
		functions,
		ifElse,
		whileLoop,
	],
	"Standard library",
	[
		preludeLib,
		fileLib,
		listLib,
		dictLib,
		stringLib,
		mathLib,
	],
])

## number of times to run each test
let n = 1
ensure(n > 0)

## let's go!
List.forEach(fun(test) ->
	if (typeOf(test) = "String") {
		print(test)
		printLn(":")
		
	} else {
		print("  ")
		print(String.rightPad(
			toString(test), 20, "."))
		
		## run it
		rep(fun(_) -> test(), n)

		printLn("OK")
	}, tests)

printLn("All tests passed!")
