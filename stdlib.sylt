
let rep(n, f) = {
	let i = 0
	while (i < n) {
		f(i)
		i <- i + 1
	}
}

let List.iter(list, f) = {
	rep(List.length(list), fun(i) ->
		f(i, list[i]))
}

let List.forEach(list, f) = {
	rep(List.length(list), fun(i) ->
		f(list[i]))
}

let List.map(list, f) = {
	let mapped = []
	List.forEach(list, fun(value) ->
		List.push(mapped, f(value))
	)
	
	mapped
}

let List.filter(list, f) = {
	let filtered = []
	List.forEach(list, fun(value) ->
		if (f(value))
			List.push(filtered, value)
	)
	
	filtered
}

let List.fold(list, init, f) = {
	let folded = init
	List.forEach(list, fun(value) ->
		folded <- f(folded, value)
	)
	
	folded
}

let List.sum(list) = {
	List.fold(list, 0, fun(sum, value) ->
		sum + value)
}

let List.flatten(list) = {
	let flat = []
	List.forEach(list, fun(item) ->
		if (typeOf(item) = "List") {
			let child = List.flatten(item)
			flat <- List.concat(flat, child)
		} else List.push(flat, item)
	)
	
	flat
}

let List.any(list, f) = {
	let loop(i) =
		if (i < List.length(list)) {
			if (f(list[i])) true
			else loop(i + 1)
		} else false
		
	loop(0)
}

let List.all(list, f) = {
	let loop(i) =
		if (i < List.length(list)) {
			if (!f(list[i])) false
			else loop(i + 1)
		} else true
		
	loop(0)
}
