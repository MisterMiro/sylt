
## test functions

let names() = {
	let value = "whatever"
	
	let name = value
	let Name = value
	let NAME = value
	let _name = value
	let _name_ = value
	let name123 = value
	let name_123 = value
	
	## names that start with keywords
	let nilpotent = value
	let true_to_life = value
	let falsetto = value
	let letter = value
	let funambulism = value
	let iffy = value
	let elsewhere = value
	let andante = value
	let oracle = value
}

let strings() = {
	let empty = ""
	let alpha = "abcDEFGHijklMNOPqrstUVWxyz"
	
	## all escape sequences...
	let hex = "\x41"
	let backslash = "\\"
	let quote = "\""
	let tab = "\t"
	let newline = "\n"
	let carriage_return = "\r"
	let null = "\0"
	
	## ...translate properly
	ensure(hex = "A")
	ensure(backslash = "\x5c")
	ensure(quote = "\x22")
	ensure(tab = "\x09")
	ensure(newline = "\x0a")
	ensure(carriage_return = "\x0d")
	ensure(null = "\x00")
}

let numbers() = {
	let integer = 123456789
	let fractional = 3.141592
}

let precedence() = {
	## multiplication > addition
	ensure(5 * 2 + 8 = 18)
	ensure(5 / 2 + 8 = 10.5)
	ensure(5 % 2 + 8 = 9)
	
	ensure(10 * 2 - 8 = 12)
	ensure(10 / 2 - 8 = -3)
	ensure(10 % 2 - 8 = -8)
	
	## addition > comparison
	ensure(1 < 1 + 1)
	ensure(1 <= 1 + 1)
	ensure(2 > 1 - 1)
	ensure(2 >= 1 - 1)
	
	## comparison > equality
	ensure(true = 1 < 2)
	ensure(true = 1 <= 2)
	ensure(true != 1 > 2)
	ensure(true != 1 >= 2)
}

let addition() = {
	let x = 8
	let y = 12
	
	## correct result
	let sum = 20
	ensure(x + y = sum)
	
	## commutative
	ensure(y + x = sum)
	ensure(x + y = y + x)
}

let subtraction() = {
	let x = 20
	let y = 12
	
	## correct result
	let difference = 8
	ensure(x - y = difference)
	
	## not commutative
	ensure(y - x != difference)
	ensure(x - y != y - x)
}

let multiplication() = {
	let x = 5
	let y = 16
	
	## correct result
	let product = 80
	ensure(x * y = product)
	
	## commutative
	ensure(y * x = product)
	ensure(x * y = y * x)
}

let division() = {
	let x = 33
	let y = 3
	
	## correct result
	let quotient = 11
	ensure(x / y = quotient)
	
	## not commutative
	ensure(y / x != quotient)
	ensure(x / y != y / x)
}

let remainder() = {
	let x = 17
	let y = 12
	
	## correct result
	let remainder = 5
	ensure(x % y = remainder)
	
	## not commutative
	ensure(y % x != remainder)
	ensure(x % y != y % x)
	
	## remainder is always positive
	ensure(7 % 3 = 1)
	ensure(7 % -3 = 1)
	ensure(-7 % 3 = 2)
	ensure(-7 % -3 = 2)
}

let unaryMinus() = {
	let x = 25
	ensure(-x = 0 - x)
	
	## double negative
	ensure(--x = x)
	
	## quadruple negative
	ensure(----x = x)
}

let arithmetic() = {
	let a = 1
	let b = a + 2
	ensure(a + b = a - (-b))
}

let comparison() = {
	let hi = 100
	let lo = -100
	
	ensure(lo < hi)
	ensure(lo <= hi)
	
	ensure(hi > lo)
	ensure(hi >= lo)
	
	let same = 70
	ensure(same <= same)
	ensure(same >= same)
}

let equality() = {
	## contains a value of each type
	let values = [
		nil,        	## Nil
		true,       	## Bool
		235,	    	## Num
		[123],	  	## List
		"'_'",	  	## String
		fun(x) -> x,	## Func
	]
	
	List.forEach(values, fun(skip, value) -> {
		## equal to itself
		ensure(value = value)
		
		## not equal to a value of any other
		## type
		List.forEach(values,
			fun(index, other) ->
				if (index != skip)
					ensure(value != other)
		)
	})
	
	## perform more type-specific tests
	ensure(true != false)
	
	ensure([] = [])
	ensure([[]] = [[]])
	ensure([0] != [1])
	
	ensure("" = "")
	ensure("ABC" != "abc")
	
	ensure(Math.pow != Math.sqrt)
}

let boolNot() = {
	ensure(true = !false)
	ensure(false = !true)
	ensure(!!true = true)
}

let boolAnd() = {
	ensure((false and false) = false)
	ensure((false and true) = false)
	ensure((true and false) = false)
	ensure((true and true) = true)
	
	## short-circuit evaluation
	let would_crash() = ensure(false)
	false and would_crash()
}

let boolOr() = {
	ensure((false or false) = false)
	ensure((false or true) = true)
	ensure((true or false) = true)
	ensure((true or true) = true)
	
	## short-circuit evaluation
	let would_crash() = ensure(false)
	true or would_crash()
}

let variables() = {
	## declaring
	let x = 70
	
	## accessing
	ensure(x = 70)
	
	## shadowing
	let x = x - 2
	ensure(x = 68)
	
	## assigning local
	x <- 14
	ensure(x = 14)
	
	## assigning open upvalue
	let y = 888
	let func() = {
		y <- 333
	}
	func()
	ensure(y = 333)
	
	## assigning closed upvalue
	let out() = {
		let z = 4
		let in() =
			z <- 900
		in
	}
	let z = out()()
	ensure(z = 900)
	
	## chaining assignments
	let a = nil
	let b = nil
	let c = nil
	a <- b <- c <- 32
	ensure(a = 32 and b = 32 and c = 32)
}

let blocks() = {
	## empty block returns nil
	let result = {}
	ensure(result = nil)
	
	## last expression is returned...
	let result = {123}
	ensure(result = 123)
	
	## ...variables do not break anything...
	let result = {
		let v1 = 13
		let v2 = 17
		123
	}
	ensure(result = 123)
	
	## ...and neither do captured values
	let c = 35
	let result = {
		let v = 9
		let sum = c + v
		123
	}
	ensure(result = 123)
	
	## assigning shadowed variable
	let x = true
	{
		let x = nil
		x <- false
		ensure(x = false)
	}
	ensure(x = true)
}

let lists() = {
	let list = [1, 2, 3]
	
	## indexing
	ensure(list[0] = 1
		and list[1] = 2
		and list[2] = 3)
		
	## negative indices count from the end
	## i.e. index -1 is equivalent to len - 1
	ensure(list[1] = list[-2])
	
	## assignment
	list[0] <- list[-1]
	list[1] <- list[-1]
	ensure(list = [3, 3, 3])
}

let functions() = {
	## empty body returns nil
	let empty() = {}
	ensure(empty() = nil)
	
	## argument passing
	let add(a, b) = a + b
	ensure(add(3, 4) = 7)
	
	## arguments + local variables
	let add(a, b) = {
		let stack_mate = 18
		a + b
	}
	ensure(add(3, 4) = 7)
	
	## accessing variable outside of
	## a functions stack window
	let outside = "outside"
	let f() = outside
	ensure(f() = outside)
	
	## moving surrounding context to
	## the heap if necessary
	let f() = {
		let keep = "jewel"
		let f2() = keep
		f2
	}
	ensure(f()() = "jewel")
	
	## anonymous functions using 'fun'
	let anon = fun(r, s, t) -> r * s * t
	ensure(anon(2, 4, 8) = 64)
	
	## recursion
	let clear(ls) =
		if (List.length(ls) > 0) {
			List.pop(ls)
			clear(ls)
		}
		
	let nums = [1, 2, 3]
	clear(nums)
	ensure(List.length(nums) = 0)
}

let ifElse() = {
	## implicit nil
	ensure((if (false) {}) = nil)
	
	## if branch
	let result = if (true) "A" else "B"
	ensure(result = "A")
	
	## else branch
	let result = if (false) "A" else "B"
	ensure(result = "B")
	
	## else if / chaining
	let i = 10
	let result =
		if (i < 0) "A"
		else if (i > 0) "B"
		else "C"
	ensure(result = "B")
}

let spamGC() = {
	let size1 = 64
	let size2 = 8
	let rec(oldList, i) = {
		if (i < size1) {
			let newList = []
			rep(size1 / size2, fun(_) ->
				List.push(newList, oldList))
			rec(newList, i + 1)
		}
	}
	
	rec([1, 2, 3, 4, 5, 6, 7, 8], 0)
}

let preludeLib() = {
	## asString
	ensure(asString(nil) = "nil")
	ensure(asString(true) = "true"
		and asString(false) = "false")
	ensure(asString(707) = "707")
	ensure(asString(["bananas", 18, true])
		= "[\"bananas\", 18, true]")
	ensure(asString(printLn) = "printLn")
	
	## asNum
	ensure(asNum("1337.25") = 1337.25)
	
	## typeOf
	ensure(typeOf(nil) = "Nil")
	ensure(typeOf(true) = "Bool")
	ensure(typeOf(1) = "Num")
	ensure(typeOf([]) = "List")
	ensure(typeOf("") = "String")
	ensure(typeOf(fun() -> {}) = "Func")
	
	## rep
	let nums = []
	rep(3, fun(i) ->
		List.push(nums, (i + 1) * 3))
	ensure(nums = [3, 6, 9])
}

let listLib() = {
	## length
	ensure(List.length([]) = 0)
	ensure(List.length([1]) = 1)
	ensure(List.length([1, 2]) = 2)
	
	## insert
	let x = [1, 3, 4]
	List.insert(x, 1, 2)
	ensure(x = [1, 2, 3, 4])
	
	List.insert(x, -1, 5)
	ensure(x = [1, 2, 3, 4, 5])
	
	let x = [1, 2, 3]
	List.insert(x, 0, 1)
	ensure(x = [1, 1, 2, 3])
	
	## del
	let x = [1, 2, 3]
	List.del(x, 1)
	ensure(x = [1, 3])
	
	List.del(x, 0)
	ensure(x = [3])
	
	List.del(x, 0)
	ensure(x = [])
	
	## push and pop
	let x = []
	
	List.push(x, 3) ## = [3]
	ensure(x = [3] and List.length(x) = 1)
	List.push(x, 7) ## = [3, 7]
	ensure(x = [3, 7] and List.length(x) = 2)
	
	List.pop(x) ## = [3]
	ensure(x = [3] and List.length(x) = 1)
	List.pop(x) ## = []
	ensure(x = [] and List.length(x) = 0)
	
	## concat
	let a = [1, 2]
	let b = [3, 4]
	let concatenated = List.concat(a, b)
	ensure(concatenated = [1, 2, 3, 4])
	
	## range
	let a = List.range(-3, 3)
	let b = [-3, -2, -1, 0, 1, 2]
	ensure(a = b)
	
	## forEach
	let indices = [0, 1, 2]
	let copy = []
	List.forEach(indices, fun(index, value) ->
	{
		ensure(index = indices[index])
		List.push(copy, value)
	})
	ensure(copy = indices)
	
	## map
	let list = List.map([4, 8, 16], 
		fun(x) -> x * 2)
	ensure(list = [8, 16, 32])
	
	## filter
	let list = [1, 0, 0, 1, 1, 0]
	list <- List.filter(list,
		fun(n) -> n = 1)
	ensure(list = [1, 1, 1])

	## flatten
	let mess = [1, [2, [3, 4]], 5]
	let clean = List.flatten(mess)
	ensure(clean = [1, 2, 3, 4, 5])
	
	## any
	let set = [true, true, false]
	ensure(List.any(set, fun(v) -> v))
	ensure(List.any(set, fun(v) -> !v))
	List.pop(set) ## = [true, true]
	ensure(List.any(set, fun(v) -> v))
	ensure(!List.any(set, fun(v) -> !v))
	
	## all
	let set = [5, 7]
	ensure(List.all(set, fun(n) -> n >= 5))
	ensure(List.all(set, fun(n) -> !(n < 5)))
	List.push(set, 3) ## = [5, 7, 3]
	ensure(!List.all(set, fun(n) -> n >= 5))
	ensure(!List.all(set, fun(n) -> !(n < 5)))
}

let stringLib() = {
	## alpha
	let str = "abcdefghijklmnopqrstuvwxyz"
	ensure(String.alpha = str)
	
	## alphaNum
	let str =
		"abcdefghijklmnopqrstuvwxyz0123456789"
	ensure(String.alphaNum = str)
	
	## digits
	let str = "0123456789"
	ensure(String.digits = str)
	
	## chars and join
	ensure(String.chars("Abc")
		= ["A", "b", "c"])
	ensure(String.join(["A", "b", "c"])
		= "Abc")
	ensure(String.join([1, "2", 34, "5"])
		= "12345")
		
	## lower and upper
	let str = "MiXeD!"
	ensure(String.lower(str) = "mixed!")
	ensure(String.upper(str) = "MIXED!")
	
	## startsWith
	ensure(String.startsWith(
		String.alpha, "abc"))
	ensure(!String.startsWith(
		String.alpha, "xyz"))
		
	## endsWith
	ensure(String.endsWith(
		String.alpha, "xyz"))
	ensure(!String.endsWith(
		String.alpha, "abc"))
		
	## trimStart
	ensure(String.trimStart("\t\n hi") = "hi")
	ensure(String.trimStart("hi") = "hi")
	
	## trimEnd
	ensure(String.trimEnd("hi \n\t") = "hi")
	ensure(String.trimEnd("hi") = "hi")
	
	## trim
	ensure(String.trim("\n\t hi \n\t") = "hi")
	ensure(String.trim("hi") = "hi")
}

let mathLib() = {
	## pi
	ensure(Math.closeTo(
		Math.pi, 3.141592, 0.00001))
	
	## euler's number
	ensure(Math.closeTo(
		Math.e, 2.718281, 0.00001))
	
	## closeTo
	let zero = 0
	let val = 0.005
	let epsilon = 0.01
	
	ensure(Math.closeTo(zero, val, epsilon))
	ensure(!Math.closeTo(zero, epsilon,
		epsilon))
	
	## numSign
	ensure(Math.numSign(-80) = -1)
	ensure(Math.numSign(0) = 0)
	ensure(Math.numSign(80) = 1)
	
	## abs
	ensure(Math.abs(-8) = 8
		and Math.abs(8) = 8)
	
	## log and pow
	let target = 8
	let bases = [
		## common
		2, Math.e, 10,
		## uncommon
		3, 14, 2000, 18000, 2000000,
	]
	
	let logRaise(base, target) = {
		let power = Math.log(target, base)
		let result = Math.pow(base, power)
		ensure(Math.closeTo(
			result, target, 0.00001))
	}
	
	List.forEach(bases, fun(_, base) ->
		logRaise(base, target))
	
	## sqrt
	ensure(Math.sqrt(256) = 16)
	ensure(Math.pow(32, 0.5) = Math.sqrt(32))
	
	## min and max
	ensure(Math.min(-1, 1) = -1)
	ensure(Math.max(-1, 1) = 1)
	
	## clamp
	let x = 2
	ensure(Math.clamp(x, -5, 5) = x)
	
	let x = 20
	ensure(Math.clamp(x, 1, 4) = 4)
	
	let x = -12
	ensure(Math.clamp(x, -8, 3) = -8)
	
	## lerp
	ensure(Math.lerp(-10, 10, 0) = -10)
	ensure(Math.lerp(-10, 10, 0.25) = -5)
	ensure(Math.lerp(-10, 10, 0.5) = 0)
	ensure(Math.lerp(-10, 10, 0.75) = 5)
	ensure(Math.lerp(-10, 10, 1) = 10)
	
	## floor, ceil, and round
	let checkRounding(n) = {
		let low = n + 0.2
		let mid = n + 0.5
		let high = n + 0.8
	
		ensure(Math.floor(low) = n)
		ensure(Math.floor(high) = n)
	
		ensure(Math.ceil(low) = n + 1)
		ensure(Math.ceil(high) = n + 1)
	
		ensure(Math.round(low) = n)
		ensure(Math.round(high) = n + 1)
	
		ensure(Math.round(mid) =
			if (n < 0) n
			else n + 1
		)
	}
	
	List.forEach([-1, 0, 1], fun(_, n)
		-> checkRounding(n))
	
	## rad and deg
	ensure(
		Math.closeTo(
			Math.rad(1), 0.0175, 0.001))
	ensure(
		Math.closeTo(
			Math.deg(1), 57.296, 0.001))
	
	## sin, cos, tan
	ensure(
		Math.closeTo(
			Math.sin(90), 0.8939, 0.001))
	ensure(
		Math.closeTo(
			Math.cos(90), -0.4480, 0.001))
	ensure(
		Math.closeTo(
			Math.tan(90), -1.9952, 0.001))
}

## tests to run

let run = List.flatten([
	"Parser",	## category name
	[			## functions to test
		names,
		strings,
		numbers,
		precedence,
	],
	"Numbers",
	[
		addition,
		subtraction,
		multiplication,
		division,
		remainder,
		unaryMinus,
		arithmetic,
	],
	"Booleans",
	[
		comparison,
		equality,
		boolNot,
		boolAnd,
		boolOr,
	],
	"General",
	[
		variables,
		blocks,
		lists,
		functions,
		ifElse,
		spamGC,
	],
	"Standard library",
	[
		preludeLib,
		listLib,
		stringLib,
		mathLib,
	],
])

## number of times to run each test
let iterations = 1
ensure(iterations > 0)

## let's go!
List.forEach(run, fun(_, value) ->
	if (typeOf(value) = "String") {
		print(value)
		printLn(":")
		
	} else {
		let name = asString(value)
		print("  ")
		print(name)
		
		## align
		let width = 20
		let len = List.length(
			String.chars(name))
		rep(width - len, fun(_) -> print("."))
		
		## run it
		rep(iterations, fun(_) -> value())
		
		printLn("OK")
	}
)

printLn("All tests passed!")
